apply plugin: 'com.android.application'

ext.versionMajor = 11
ext.versionMinor = 0
ext.versionPatch = 0

ext.minimumSdkVersion = 21
ext.supportLibrariesVersion = '25.3.1'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.0'

    defaultConfig {
        applicationId "com.sidzi.circleofmusic"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 26
        versionCode generateVersionCode()
        versionName getVersionName()
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String getVersionName() {
    String[] versionNames = ["A", "B", "Chimes", "Drums", "Ektara", "Flute", "Guitar", "Harpsichord", "Igil", "J", "Keyboard", "Longhorn", "M", "N", "O"]
    return versionNames[ext.versionMajor - 1]
}


dependencies {
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'net.gotev:uploadservice:3.1'
    compile 'com.j256.ormlite:ormlite-android:5.0'
    compile 'com.j256.ormlite:ormlite-core:5.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile "com.android.support:appcompat-v7:$supportLibrariesVersion"
    compile "com.android.support:support-v13:$supportLibrariesVersion"
    compile "com.android.support:design:$supportLibrariesVersion"
    compile "com.android.support:recyclerview-v7:$supportLibrariesVersion"
    compile "com.android.support:support-v4:$supportLibrariesVersion"
}
